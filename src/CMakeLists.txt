# Header files need not be enumerated
INCLUDE_DIRECTORIES(${CMAKE_CURRENT_SOURCE_DIR} ${PROJECT_BINARY_DIR})

###########
# LIB RANKSTR #
###########

# Install header files
LIST(APPEND librankstr_install_headers
    rankstr_mpi.h
)
INSTALL(FILES ${librankstr_install_headers} DESTINATION ${CMAKE_INSTALL_INCLUDEDIR})

LIST(APPEND librankstr_srcs
    rankstr_mpi.c
)

# RANKSTR Library
ADD_LIBRARY(rankstr_o OBJECT ${librankstr_srcs})
TARGET_LINK_LIBRARIES(rankstr_o PRIVATE MPI::MPI_C)

IF(BUILD_SHARED_LIBS)
   ADD_LIBRARY(rankstr SHARED $<TARGET_OBJECTS:rankstr_o>)
   ADD_LIBRARY(rankstr::rankstr ALIAS rankstr)
   TARGET_LINK_LIBRARIES(rankstr PUBLIC MPI::MPI_C)
   TARGET_INCLUDE_DIRECTORIES(rankstr PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}> $<INSTALL_INTERFACE:include>)
   SET_TARGET_PROPERTIES(rankstr PROPERTIES OUTPUT_NAME rankstr CLEAN_DIRECT_OUTPUT 1)
   INSTALL(TARGETS rankstr EXPORT rankstrTargets LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR})
ENDIF()

ADD_LIBRARY(rankstr-static STATIC $<TARGET_OBJECTS:rankstr_o>)
ADD_LIBRARY(rankstr::rankstr-static ALIAS rankstr-static)
IF(RANKSTR_LINK_STATIC)
   SET_TARGET_PROPERTIES(rankstr-static PROPERTIES LINK_SEARCH_START_STATIC 1)
   SET_TARGET_PROPERTIES(rankstr-static PROPERTIES LINK_SEARCH_END_STATIC 1)
ENDIF(RANKSTR_LINK_STATIC)
TARGET_LINK_LIBRARIES(rankstr-static PUBLIC MPI::MPI_C)
TARGET_INCLUDE_DIRECTORIES(rankstr-static PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}> $<INSTALL_INTERFACE:include>)

SET_TARGET_PROPERTIES(rankstr-static PROPERTIES OUTPUT_NAME rankstr CLEAN_DIRECT_OUTPUT 1)
INSTALL(TARGETS rankstr-static EXPORT rankstrTargets LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR})
